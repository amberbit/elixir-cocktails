# Decoupling form validation logic from Ecto models

## The problem

Suppose you want to move form validation logic out of your Ecto model in
Phoenix app. Your model is growing, and you would like
to split it into a few smaller modules based on functionality to avoid unneeded
complexity and confusion.

## The solution

We will move changeset function out of the model. As `Ecto.Model`
is in fact an "umbrella" module containing different modules, we can separate them across files.

### Creating separate module for changeset

We have a simple app with just one "Book" resource (generated by
Phoenix.gen.html mix task). We are going to move changeset function out of
`web/models/book.ex` into a new file `lib/form_validation_app/book_validation.ex`.
Changeset will be part of a new module, which we will name
`BookValidation`.
Let's create our module and put changeset function
inside along with adding some example validations.

    defmodule FormValidationApp.BookValidation do
      import Ecto.Changeset

      @required_fields ~w(title isbn pages)
      @optional_fields ~w(description)

      def changeset(model, params \\ :empty) do
        model
          |> cast(params, @required_fields, @optional_fields)
          |> validate_length(:title, max: 30)
          |> validate_number(:pages, greater_than: 10)
          |> validate_format(:isbn, ~r/\d*-\d*-\d*/)
          |> validate_length(:isbn, is: 15)
        end
      end

We are importing `Ecto.Changeset` instead of whole `Ecto.Model`, because it is the only module needed
to provide functionality for casting and validating params.
We have moved our changeset function directly from model to our new
module along with required and optional fields lists, as they are used
only by changeset and won't be needed in our model anymore.

### Updating controller

Now we need to update the controller, so it won't look for changeset in
`Book` module anymore.

Let's alias our new module in `web/controllers/book_controller.ex` for more
convenient use by adding following line:

    alias FormValidationApp.BookValidation

Now it's time to update every changeset function call by changing

    Book.changeset

to

    BookValidation.changeset

And that's it. We have moved our form validation logic completely out of
model.

### Cleaning model

Your model should now look something like that:

    defmodule FormValidationApp.Book do
      use FormValidationApp.Web, :model

      schema "books" do
        field :title, :string
        field :description, :string
        field :isbn, :string
        field :pages, :integer

        timestamps
      end

    end

It's already looking quite simple. But we moved changeset out, so we
don't need to use whole `Ecto.Model` anymore.
Well then, what do we need?

It's obvious that we need `Ecto.Schema`. There is even word "schema" in
our module, so that one was easy.

We moved our changeset into another model, so we definitely don't need
`Ecto.Changeset` anymore.

We aren't really using any queries in our example app, so let's skip
`Ecto.Query` as well.

Actually, we need only two more modules at this point:
`Ecto.Model.Callbacks` (providing lifecycle callbacks to our model) and
`Ecto.Model.Timestamps` (automatically setting `inserted_at` and
`updated_at` fields).

Knowing that, we can replace

    use FormValidationApp.Web, :model

with

    use Ecto.Schema
    use Ecto.Model.Timestamps
    use Ecto.Model.Callbacks

in our model.

Of course, you may need more modules, if your model is more complex. For
example if you have any associations defined, you are most probably going to need
`Ecto.Model.Dependent`.

That's it. Now our `Book` module is only responsible for accessing the
database, and `BookValidation` is providing all the validation logic
through a changeset.

## Extra tips

You can find list of all modules included in `Ecto.Model` in [Phoenix documentation](http://hexdocs.pm/ecto/Ecto.Model.html).

Contributors:

[Dominika Kruk](mailto:dominika.kruk@amberbit.com)
